config {
  type: "table",
  schema: "DWHDF",
  name: "TLDB1_TAX_VAL",
  tags: ["scontrini"],
  description: "Questa tabella gestisce il calcolo dell'IVA basandosi sulle VAT_RULES definite nella colonna VAT_RULE della tabella TDWD1. Da questa tabella viene calcolato il valore del campo LDB1_TAX_IVA_LOC_VAL ",
  columns: {
    ID_RGH: "",
    ID_SCN: "",
    TAX: ""
  }
}

SELECT
  TLDB1_INPUT.ID_SCN,
  TLDB1_INPUT.ID_RGH,
  CASE
    WHEN MAX(TLDB1_INPUT.RULE_COD) = "2" THEN SUM(ABS(COALESCE(KONV_SAL.KONV_KWERT, 0))) / MAX(TLDB1_INPUT.FATT_CAMB)
    ELSE SUM(ABS(COALESCE(KONV_SAL.KONV_KBETR, 0))) * COALESCE(
      MAX(TLDB1_VALUTE.FATTORE_CORRETTIVO_FATTURAZIONE),
      1
    )
  END AS TAX
FROM
  ${ref("TLDB1_INPUT")} TLDB1_INPUT
  LEFT JOIN ${PARTITION.ref(ref("KONV_SAL"), "SAL")} KONV_SAL ON TLDB1_INPUT.TEMP_PER_VALORI = KONV_SAL.KONV_KNUMV
  AND TLDB1_INPUT.ID_RGH_STR = KONV_SAL.KONV_KPOSN
  AND KONV_SAL.KONV_KSCHL IN UNNEST(SPLIT(TLDB1_INPUT.RULE, ','))
  LEFT JOIN ${PARTITION.ref(ref("ZINT_VAT_SAL"), "SAL")} ZINT_VAT_SAL ON KONV_SAL.KONV_KSCHL = ZINT_VAT_SAL.ZINT_VAT_KSCHL
  AND KONV_SAL.KONV_MWSK1 = ZINT_VAT_SAL.ZINT_VAT_MWSKZ
  AND TLDB1_INPUT.STATO_COD = ZINT_VAT_SAL.ZINT_VAT_LAND1
  LEFT JOIN ${ref("TLDB1_VALUTE")} TLDB1_VALUTE ON TLDB1_VALUTE.ID_SCN = TLDB1_INPUT.ID_SCN
  AND CAST(TLDB1_VALUTE.ID_RGH AS NUMERIC) = TLDB1_INPUT.ID_RGH
GROUP BY
  TLDB1_INPUT.ID_SCN,
  TLDB1_INPUT.ID_RGH
